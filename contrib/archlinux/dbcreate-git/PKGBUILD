# Maintainer: Abdulazeez Sherif <abdoulazeezx@gmail.com>

pkgname=dbcreate-git
pkgver=VERSION
pkgrel=1
pkgdesc='Automated Database Creation for Django Supported Databases'
arch=('any')
url='https://github.com/thisisazeez/dbcreate'
license=('Apache')
depends=()
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-poetry-core'
  'python-wheel'
)
checkdepends=('python-pytest')
provides=('dbcreate')
conflicts=('dbcreate')
options=('!strip')

source=(
  "${pkgname}::git+https://github.com/thisisazeez/dbcreate.git"
)

sha512sums=(
  'SKIP'
)

pkgver() {
  printf "%s.r%s.%s" \
    "$(poetry version -s)" \
    "$(git -C "${pkgname}" rev-list --count HEAD)" \
    "$(git -C "${pkgname}" rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${pkgname}"
  python -m build --wheel --no-isolation
}

check() {
  cd "${srcdir}/${pkgname}"
  python -m pytest
}

package() {
  cd "${srcdir}/${pkgname}"

  echo >&2 'Packaging the wheel'
  python -I -m installer --destdir="${pkgdir}" dist/*.whl

  echo >&2 'Packaging README.md'
  install -D -m 644 -t "${pkgdir}/usr/share/doc/${pkgname}" \
    'README.md'

  echo >&2 'Packaging the license'
  install -D -m 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" \
    'LICENSE'
}
